
# SentryWire Field Manual
### Section Index
1. [**Common KQL Search Syntax**](#common-kql-searches)
1. [**Investigator Event Log Indicies**](https://github.com/Sentry-Wire/SentryWire-FieldManual/blob/main/SentryWire_Field_Manual.md#index-types)
2. [**Investigator Event Log Indicies**](#index-types) https://github.com/Sentry-Wire/SentryWire-FieldManual/blob/main/SentryWire_Field_Manual.md#index-types
1. [**Example Searches by Event Log Type**](#network-alerts)
1. [**KQL Query Operators and Syntax**](#kql-searches)
1. [**Direct Packet Searches**)](#pcap-searches)



### Network Event Log Types Generated by SentryWire
Available to search within the Investigator (Kibana) and send to external log collectors via syslog and other methods

| Available Event Logs | KQL |
| :------ | ------ |
| Active Triggers | `event_type:activetrigger` | 
| [Flow Records](#flow) | `event_type:flow` | 
| [IDS Alerts](#network-alerts) | `event_type:alert`| 
| [Anomaly](#anomaly) | `event_type:anomaly`| 
| [HTTP](#http) | `event_type:http` | 
| [SMTP](#smtp) | `event_type:smtp` | 
| [SMB](#smb) | `event_type:smb` | 
| [Files](#fileinfo) | `event_type:fileinfo` | 
| [DNS](#dns) | `event_type:dns` | 
| [TLS](#tls) | `event_type:tls` | 
| DHCP | `event_type:dhcp` | 
| DHCPv6 | `event_type:dhcpv6` | 


| Systemalert Logs |
| :------ |
| Not available in the investigator view |
| Accessible via the Administrator view and forward off the system to external log collectors via syslog or other methods | 

## Immutable Fields 
### Present Across Each Event Log Type
| Field | Description | KQL Filter |
| :------ | ------ | ------ |
| timestamp | Timestamp of first packet in flow | n/a |
| defended | If event contains a defined critical asset or service from SentryWire Augmentation Policies: True or False  | `defended:true` or `defended:false` |
| ioc | Field contains IOC attributes from user defined SentryWire Augmentation policies| `ioc:CVE-2016-0128,192.168.5.47` |
| suspected | True or False, searchable field for aggregating any/all IOCs | `suspected:true` or `suspected:false` |
| src_ip | Source host IP address | `src_ip:26.0.0.180` |
| src_port | Source host TCP/UDP port | `src_port:80` |
| dest_ip | Destination endpoint IP address | `dest_ip:58.0.181.128` |
| dest_port | Destination endpoint TCP/UDP port | `dest_port:50704` |
| proto | Transport layer protocol of connection | `proto:tcp`  `proto:udp` `proto:icmp` `proto:IPv6-ICMP` `etc` |
| app_proto | Dynamic application protocol detection, not always present | `app_proto:http` `app_proto:smtp` `app_proto:dns` `app_proto:smb` `etc` |
| event_type| Information for each  parsed packet, some fields are unique to the event_type| `event_type:fileinfo` `event_type:http` `etc` |
| dlink_key | Immediate retrieval of packets related the event of interest as a standard PCAP  | `Get PCAP` |
| dsearch_key | Initiates a search for viewing packets and objects in SentryWire's UI without having to download a PCAP | `PCAP+Metadata` |
| flow_id | A unique identifier for a flow, multiple event_type logs can be present fora single flow_id | `flow_id:521380886474878` |
| nodename | SentryWire nodename that the event log data is from, used for FM and GFM deployments | `nodename:swpocbox1` |
| groupname | SentryWire Global Federation Manager (GFM) logically defined name for a user defined grouping of one or more capture nodes | `groupname:g1` |

### Index Types
Select specfic index by using the supplied dropdown form field in the provided SentryWire dashboards or from the Discover index slection dropdown in the upper left corner below the search bar.

| Available Event Indeices | non-GFM | GFM |
| :------ | ------ | ------ |
| All Indices | inv_* | swclusternode*:inv_* | 
| Flow Records | inv_flow_* | swclusternode*:inv_flow_* |
| IDS Alerts | inv_alert_* | swclusternode*:inv_alert_* |
| Anomaly | inv_anomaly_* | swclusternode*:inv_anomaly_*|
| HTTP | inv_http_* | swclusternode*:inv_http_* |
| SMTP | inv_smtp_* | swclusternode*:inv_smtp_* |
| SMB | inv_smb_* | swclusternode*:inv_smb_* |
| Files | inv_fileinfo_* | swclusternode*:inv_fileinfo_* |
| DNS | inv_dns_* | swclusternode*:inv_dns_* | 
| TLS | inv_tls_* | swclusternode*:inv_tls_* |
| DHCP | inv_dhcp_* | swclusternode*:inv_dhcp_* |
| DHCPv6 | inv_dhcpv6_* | swclusternode*:inv_dhcpv6_* |

### Common KQL Searches

**Basic IP, Port, Application Protocol, and "exists"**
1. Index: Any
1. Investigator Landing Page
1. Any Dashboard
1. Discover
  - `src_ip: 58.0.181.128 and dest_ip: 80.79.200.11 and app_proto: http and (dest_port: 80 or 8080)`
  - Search for an IP or Port that could be source or destination: `*_ip:172.16.9.171 and *_ip: 80.79.200.11 and (*_port: 80 or 8080)`
  - Search for a value in any field where a value exists: `src_ip: *` | `file.filename:*` | `alert.metadata.malware_family: * `

**In release version 7.3.0.309-408.14r2.41 we applied IP field type mappings to both the `src_ip` and `dest_ip` fields. This has enabled users to search and aggregate visualizations by IPv4 or IPv6 CIDR notations**
  - `src_ip: 10.2.22.0/24`
  - `dest_ip: 192.168.0.0/16`
  - `*_ip:172.16.0.0/12`
  - `src_ip: 10.0.0.0/8 or 192.168.0.0/16 or 172.16.0.0/12`

## Network Alerts
1. Index: Index: Index: inv_alert_* or swclusternode*:inv_alert_*
1. Investigator Landing Page
1. Alert Dashboard
1. Threat Hunting Dashboard
1. Discover
- Search for an IP(any direction) that has network alert events by severity on a specific port: `*_ip:10.8.34.8 and alert.severity: 3 and src_port:80 or 8080`
- Search for files that were successfully downloaded, where the file magic and extension don't match: `dest_ip: 10.3.29.101 and file.magic: pe32 and not file.name: pe32 and http.status: 200`
- Search for network alert for Emotet Malware that includes a defined critical asset: `defended:true and emotet`
_Note that critical assets are defined by user upload from the Admin UI or via API_
- Search for any network alerts flagged with potential malware across multiple internal IPv4 address blocks: `src_ip: 10.0.0.0/8 or 192.168.0.0/16 or 172.16.0.0/12 and alert.signature: malware` 

```
- alert.category: Web Application Attack
- alert.metadata.affected_product: Web_Server_Applications
- alert.metadata.attack_target: Server
- alert.metadata.deployment: Datacenter
- alert.metadata.mitre_tactic_id: TA0011
- alert.metadata.mitre_tactic_name: Command_And_Control
- alert.metadata.mitre_technique_id: T1041
- alert.metadata.mitre_technique_name: Exfiltration_Over_C2_Channel
- alert.metadata.signature_severity: Major
- alert.metadata.tag: c2
- alert.severity: 3
- alert.signature: ET MALWARE W32/Asprox.ClickFraudBot CnC Beacon Acknowledgement
- alert.signature_id: 2018097
- file.filename: 987978.exe
- file.magic: PE32 executable (GUI) Intel 80386, for MS Windows
- file.size: 43298
- http.hostname:translampung.com
- http.http_content_type:application/octet-stream
- http.http_method:GET
- http.status:200
- http.url:/xkIJX5Lp/
```

[Back to Top](#sentrywire-field-manual)

## DNS
1. Index: Index: inv_dns_* or swclusternode*:inv_dns_*
1. Investigator Landing Page
1. Discover
- Search DNS response types: `dns.rrtype: TXT` or `dns.rrtype: A`
- Search DNS type: `dns.type: query` or `dns.type: answer`

```
- dns.rrname: sp.adbrn.com
- dns.rrtype: A
- dns.type: query
- dns.type: answer
- dns.answers: {"rrname":"sp.adbrn.com","rrtype":"CNAME","ttl":"490","rdata":"sp-adbrn.datacratic-px.com"},{"rrname":"sp-adbrn.datacratic-px.com","rrtype":"CNAME","ttl":"67","rdata":"lb.dony1.pxl.datacratic-px.com"},{"rrtype":"A","ttl":"67","rdata":"138.197.231.80","rrname":"lb.dony1.pxl.datacratic-px.com"}
- dns.grouped.A: 138.197.231.80, 138.197.231.128, 138.197.231.117, 138.197.231.30, 159.203.159.211, 138.197.231.131, 138.197.231.27, 138.197.231.130
- dns.grouped.CNAME: sp-adbrn.datacratic-px.com, lb.dony1.pxl.datacratic-px.com
- dns.grouped.TXT: AVG=16 TIME=01610171507 IAVI=10467 CORE=4392 XPLSC=2992 XPLSB2=0155 XPLSB=1276 XPLCF=0012 XPLPH=0014 WIN=6086
```
[Back to Top](#sentrywire-field-manual)

## Fileinfo
**Search for File Artifacts**
1. Index: inv_fileinfo_* or swclusternode*:inv_fileinfo_*
1. Investigator Landing Page
1. Fileinfo Dashboard
1. Discover
  - Search by extension using a preceeding wildcard: `file.filename: *.png or *.gif`
  - Add IP: `dest_ip: 172.16.9.171 AND (file.filename: *.png or *.gif)`
  - Size based range queries: `file.size >= 1300` or `file.size <= 1300` | `file.size <= 1300 AND dest_ip: 172.16.9.171 AND (file.filename: *.png or *.gif)`
  
  _A note regarding file.size: the field is in bytes. A file.size value of 1300 would be 1.3Kb and a vlaue of 13000000 would be 13Mb_
  - Add HTTP Method and HTTP Status Code 200: `dest_ip: 172.16.9.171 AND file.filename: *.png and http.http_method: GET and http.status: 200`
  - Search with filemagic: `file.magic: "jpeg"` 
  - Both the file.magic and file.filename fields are assigned a text field mapping and enables imprecise/not-exact string matches that allow for searches such as `file.magic: PE32` or `file.filename: DLL` within the fileinfo index you can also freehand a search without specifying an event field (not reccomended for searches larger then a few hours) `PE32` or `PE32 and file.filename: *.png` to match potentially malicous executable files that are coded to present as PNGs
  - SentryWire includes relevant http event information in the fileinfo event log metadata it generates, see a sample fileinfo event log below:

**event_type:fileinfo unique fields**
```
- file.filename: /wp-content/thumbnails/featured-thumbnail/c6073096fac028e_280_190.jpg
- file.magic: JPEG image data, JFIF standard 1.01, comment: "CREATOR: gd-jpeg v1.0 (using IJG JPEG v62), quality = 100"
- file.sha256: bb5d813cbab0465924ce433ce979e6396e2fb8e5488675d6cdae934fcfbbdeb3
- file.size: 17151
- http.client.device.name: Other
- http.client.name: Chrome
- http.hostname: 3432.voxcdn.com
- http.http_content_type: image/jpeg
- http.http_method: GET
- http.http_refer: http://www.barstoolsports.com/
- http.http_user_agent: Mozilla/5.0 (Windows NT 6.1; WOW64) AppleWebKit/537.17 (KHTML, like Gecko) Chrome/24.0.1312.57 Safari/537.17
- http.length: 17151
- http.protocol: HTTP/1.1
- http.status: 200
- http.url: //wp-content/thumbnails/featured-thumbnail/c6073096fac028e_280_190.jpg
```
[Back to Top](#sentrywire-field-manual)

### HTTP
**HTTP Searches**

_Note: HTTP METHOD, HTTP STATUS CODE, and HTTP PROTOCOL are located in the fileinfo event logs. Use the swclusternode*:inv_* index to combine searches for METHOD/STATUS/PROTOCOL with HTTP fields listed below_

1. Index: inv_http_* or swclusternode*:inv_http_*
1. Investigator Landing Page
1. HTTP Dashboard
1. Discover
- Search for HTTP network events by content type: `http.http_content_type: image/x-icon` or 
- Search for HTTP events containing compressed files where destinaiton is outside the US: `not geo_dest_ip.country_iso_code: US and http.http_content_type: "application/zip" or rar or gzip`
- Search for HTTP user-agent:`http_user_agent: Mozilla or Microsoft`
- Search for a specific HTTP user-agent: `http_user_agent: "Mozilla/5.0 (Windows NT 10.0; Win64; x64) AppleWebKit/537.36 (KHTML, like Gecko) Chrome/80.0.3987.132 Safari/537.36 Edg/80.0.361.66"`
_Note the use of double quotes when searching for a specific user-agent_


**Unique Fields `event_type:http`**
```
- http.client.device.name: Other
- http.client.name: Chrome
- http.client.original: Mozilla/5.0 (Windows NT 5.1; WOW64) AppleWebKit/537.36 (KHTML, like Gecko) Chrome/53.0.2832.7 Safari/537.36
- http.client.os.full: Windows XP
- http.client.os.name: Windows
- http.client.os.version: XP
- http.client.version: 53.0.2832.7
- http.hostname: scuritybusiness.com
- http.http_content_type: application/javascript
- http.http_port: 443
- http.http_user_agent: Mozilla/5.0 (Windows NT 5.1; WOW64) AppleWebKit/537.36 (KHTML, like Gecko) Chrome/53.0.2832.7 Safari/537.36
- http.url: /jquery-3.2.2.min.js
```
[Back to Top](#sentrywire-field-manual)

### SMB
**SMB Searches**
1. Index: inv_smb_* or swclusternode*:inv_smb_*
1. Investigator Landing Page
1. SMB Dashboard
1. Discover
  - Search by file extension: `smb.filename : *.exe`
  - Search for specific shares:`IPC$` or `ADMIN$` or `C$`
  - Search for default remote sharetype activity: `IPC$ and smb.share_type:PIPE and smb.status:STATUS_SUCCESS`

**event_type:smb unique fields**
```
- smb.command: SMB2_COMMAND_TREE_CONNECT
- smb.dialect: 2.02
- smb.id:3
- smb.named_pipe: \\thing1-thing2\IPC$
- smb.session_id: 4599238885473
- smb.share_type: PIPE
- smb.statua: STATUS_SUCCESS
- smb.status_code: 0x0
- smb.ntlmssp.domain: STORMRUNCREEK
- smb.ntlmssp.host: DESKTOP-C901YOC
- smb.ntlmssp.user: roderick.furf
```
[Back to Top](#sentrywire-field-manual)

### SMTP
**SMTP Searches**
1. Index: inv_smtp_* or swclusternode*:inv_smtp_*
1. Investigator Landing Page
1. SMTP Dashboard
1. Discover
  - Search by mail from: `smtp.mail_from: events@nutanix.com>`
  - Search by mail from and to:`email.from: "events@nutanix.com" and email.to:"some.person@somecompany.com"`
  _A Note about using double quotes in the example above, enclosing the sender and recepient fields in quotes forces the seach to bring back only results matching what is contained in the double quotes. Running the search without double quotes will default to the search matching any text in the results, you can try it without double quotes and see the difference._
  - Search for emails containing attachements by domain address: `email.attachment : * and email.to: gmail.com`
  - Search for specific email attachments by sender: `email.to: "xsdfddeasg@gmail.com" and email.attachment: paythisinvoice.xlsx`
  - Search for specific email attachments by sender: that were received: `email.to: "xsdfddeasg@gmail.com" and email.attachment: paythisinvoice.xlsx and email.received: *`
  - Search for a specific x-mailer in the header: `JAS AolApp/3.9.0.7-Android`

**event_type:smtp unique fields**
```
- email.attachment: =?UTF-8?B?Q2FsY3VsYXRpb24tyMS56aXA=?=
- email.from: <niweqwey@kek.att.ne.jp>
- email.cc: Some Person <some.other.person@some.other.company.com>
- email.received:	by omptrans.emails.xfinity.com id huhbcq1hdb0l for <some.person@some-company.com>; Tue, 4 Oct 2016 18:26:36 -0700 (envelope-from <comcast.53457@enfgs.qtest2.com>), from omptrans.emails.xfinity.com (omptrans.emails.xfinity.com [12.12.12.229]) by us-smtp-1.mimecast.com with ESMTP id us-mta-4-awsMmGjjdHasfL-1; Tue, 04 Oct 2016 21:26:37 -040
- email.subject: Hi frend. Please Kind review attachd file 
- email.to: bardfas@mega.nz
- email.url: yourbestlove.ru?213gen
- email.x_mailer: Microsoft Outlook 16.0
smtp.helo: localhost
smtp.mail_from: <niweqwey@kek.att.ne.jp>
smtp.rcpt_to: <bardfas@mega.nz>
```
[Back to Top](#sentrywire-field-manual)

# General Information

### Flow ID: A Unique Identifier for Easy Pivoting
 SentryWire leverages Suricata as more than just an IDS to generate alert logs, it is a robust packet processing framework with an extensible logging engine. It processes every packet SentryWire captures and parses the protocol data - including flow records and transaction logs. Even when no IDS rules are enabled.

| Time | src_ip | src_port | dest_ip| dest_port | flow_id | event_type |
| ------ | ------ | ------ | ------ |------ |------ |------ |
| Aug 17, 2022 @ 00:46:49 | 26.0.0.180 | 50704 | 58.0.181.128 | 80 |`521380886474878` | flow |
| Aug 17, 2022 @ 00:46:49 | 58.0.181.128  | 80 | 26.0.0.180 | 50704  | `521380886474878` | fileinfo |
| Aug 17, 2022 @ 00:46:49 | 26.0.0.180 | 50704 | 58.0.181.128 | 80 | `521380886474878` | http |

Each log record -- be it an alert log or a specific protocol like HTTP, TLS, SMB, DNS, etc -- has a flow_id record. That flow_id is unique for the whole session/flow. All logs from that flow share the same ID. For example when Suricata generates an alert associated with a visit to a specific web page, it also generates HTTP transaction logs for that flow along with the file transactions, user agents, URLs, full HTTP headers and more. 

> **All of the event type logs that are generated from the same flow are linked by a `flow_id` that can be used as a search filter to provide more context around a an initial event of interest.**

```
KQL Filter:
flow_id:521380886474878
```
[Back to Top](#sentrywire-field-manual)

### CommunityID
##### Standardized Flow HashingPivoting To or From Another Network Security Monitoring (NSM) Appliance 

> When processing flow data from a variety of monitoring applications (such as Zeek, Suricata, and WireShark), it's often desirable to pivot quickly from one dataset to another. While the required flow tuple information is usually present in the datasets, the details of such "joins" can be tedious, particular in corner cases[^1].

Community ID flow hashing standardizes the production of a string identifier representing a given network flow, to reduce the pivot to a simple string comparison.[^3] 

```
KQL Filter:
community_id:7GDdSoonhZgYeF24IyIfrKId6yg=
```

# KQL Searches
### Investigator: KQL Search Filters and Expressions[^2]
The Kibana Query Language (KQL) is a simple syntax for filtering Elasticsearch data using free text search or field-based search. KQL is able to suggest field names, values, and operators as you type.

KQL has a different set of features than the Lucene query syntax. KQL is able to query nested fields and scripted fields. KQL does not support regular expressions or searching with fuzzy terms.

|  Terms Query  Boolean Operators | Range Query Operators | 
| :------: | :------: |
| and | > |
| or | >= |
| not | < |
| and not | =< |

**Terms Query**: A terms query uses exact search terms. To query using exact search terms, enter the field name followed by `:` and then the values separated by explicit boolean operators.


`http.status:301` matches events where the http.status field is present and matches the value 301

We can make terms required by using `and`.

`http.status:200 and http.http_method:GET` will match events where the status matches 200 and the method matches GET. 

`http.status:200 and http.http_method:GET or http.http_method:POST` will match documents where the response is 200 and method is GET OR events where the method is POST and the response is anything, including not present.
*This is because by default `and` has a higher precedence than `or`*

You can override the default precedence with grouping:

`http.status:200 and (http.http_method:GET or http.http_method:POST)` will match events where the status is 200 and method is either GET or POST.

Terms can be excluded by prefixing them with `not`: `not http.status:200` will match events where the status is not 200. This also works with grouping: `http.status:200 and not (http.http_method:GET or http.http_method:POST)`

**Range Queries**
`http.status:200 and file.size >= 1300` will find events with status 200 and with file sizes that are greater than or equal to 1300bytes (1.3KB). 
**Important Note: a _colon `:`_ is not required in range queries**

**Exists Queries**
Exist queries are simple and do not require a special operator, `email.attachment:*` will find all events where an email log event has an attachment.

**Wildcard Queries**
The SentryWire Investigator allows both preceding and trailing wildcard queries, `http.client.os.name:win*` would match events where the `http.client.os.name` field starts with "win", which would match values like "windows 7" and "windows xp".

Wildcards also allow us to search multiple fields at once. This can come in handy when you have similar versions of the same event field. Letâ€™s say we have `http.client.os.name` and `http.client.os.full` fields and we want to check both for the term "windows xp". We can do it like this: `http.client.os*:windows xp` and get the fields below back that includes a third field match:

| `http.client.os*:windows xp` Returns: | 
| ------- |
| http.client.os.full: Windows XP |
| http.client.os.name: Windows |
| http.client.os.version: XP |

**Nested field support**
Normally with nested fields in Kibana, consideration on how to match parts of the nested query to the individual nested documents takes a little work, but with the SentryWire Investigator we have explicitly mapped the inner nested fields to make life just a little easier!

In the following event, dns.answers is a nested field that includes rdata, rrname, ttl, and a rrtype:

```
dns.answers
{
  "rdata": "100.100.100.100",
  "rrname": "www.cisco.com",
  "ttl": 30000,
  "rrtype": "A"
}
```
We will match a single nested field by searching for what is an abnormally large TTL for a dns return type A by simply adding the ttl to the dns.answers field like so: `dns.answers.ttl >= 300`, every other field in the nested event would be treated as a standard term query like so: 
- `dns.answers.rdata:100.100.100.100` 
- `dns.answers.rrname:www.cisco.com` 
- `dns.answers.rrtype:A`

[Back to Top](#sentrywire-field-manual)

# PCAP Searches
### Information regarding searches using BPF or KQL packet based searches, these searches are against the packet store and are not executed in Elasticsearch.

[Back to Top](#sentrywire-field-manual)
